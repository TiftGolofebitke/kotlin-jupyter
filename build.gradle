
buildscript {
    ext.shadowJarVersion = "5.2.0"
    //ext.kotlinVersion = '1.3.60-dev-2180-83'
    //ext.kotlinVersion = '1.3.70-dev-1590'
    ext.kotlinVersion = '1.3-SNAPSHOT'
    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadowJarVersion"
    }
}

allprojects {
    apply plugin: 'kotlin'

    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
//         only when using Kotlin EAP releases ...
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
        maven { url 'https://kotlin.bintray.com/kotlin-dependencies' }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

        testCompile 'junit:junit:4.12'
    }

    version = '0.6.3'

    ext.installPath = project.hasProperty('installPath') ? project.getProperty('installPath') : "${System.properties['user.home']}/.ipython/kernels/kotlin"

    ext.debuggerConfig = "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=1044"
}

apply plugin: 'com.github.johnrengelman.shadow'

configurations {
    deploy
}

dependencies {
    compile project(":jupyter-lib")
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-scripting-jvm-host-embeddable:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-scripting-common:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-scripting-compiler-embeddable:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-compiler-embeddable:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-script-util:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-main-kts:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-scripting-dependencies:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-scripting-dependencies-maven:$kotlinVersion"

    compile "org.apache.maven:maven-core:3.0.3"
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'org.zeromq:jeromq:0.3.5'
    compile 'com.beust:klaxon:2.1.8'
    runtime 'org.slf4j:slf4j-simple:1.7.25'
    runtime "org.jetbrains.kotlin:jcabi-aether:1.0-dev-3"
    runtime "org.sonatype.aether:aether-api:1.13.1"
    runtime "net.java.dev.jna:jna:5.4.0"

    deploy project(":jupyter-lib")
}

jar.manifest.attributes(
    'Main-class': 'org.jetbrains.kotlin.jupyter.IkotlinKt',
    'Implementation-Version': version
)

shadowJar {
    baseName = 'kotlin-jupyter-kernel'
    classifier = ''
    mergeServiceFiles()
}

task cleanInstallDir(){
    doLast {
        File installDir = new File("$installPath")
        installDir.deleteDir()
    }
}

task installKernel(type: Copy, dependsOn: [cleanInstallDir, shadowJar]) {
    from shadowJar.outputs
    into installPath
}

void createTaskForSpecs(Boolean debug) {
    String taskName = debug ? "createDebugSpecs" : "createSpecs"
    task(taskName) {
        dependsOn cleanInstallDir
        doLast {
            String spec = new File("kernelspec/kernel.json.template").getText('UTF-8')
            File kernelFile = files { shadowJar }.singleFile
            spec = spec.replace("\${KERNEL_JAR_PATH}", "$installPath/${kernelFile.name}")
            String libsCp = files { configurations.deploy }.files.collect {
                "$installPath/${it.name}"
            } .join(File.pathSeparator)
            spec = spec.replace("\${RUNTIME_CLASSPATH}", libsCp)
                    .replace("\${DEBUGGER_CONFIG}", debug ? "\"$debuggerConfig\"," : "")
                    .replace("\${LIBRARIES_PATH}", "$installPath/libraries.json")
            File installDir = new File("$installPath")
            if (!installDir.exists()) {
                installDir.mkdirs();
            }
            new File("$installPath/kernel.json").write(spec, 'UTF-8')
        }
    }
}

task copyLibrariesConfig(type: Copy, dependsOn: cleanInstallDir) {
    from "libraries.json"
    into installPath
}

createTaskForSpecs(true)
createTaskForSpecs(false)

task installLibs(type: Copy, dependsOn: cleanInstallDir) {
    into "$installPath"
    from configurations.deploy
}

task install(dependsOn: [installKernel, installLibs, createSpecs, copyLibrariesConfig]) {
}

task installDebug(dependsOn: [installKernel, installLibs, createDebugSpecs, copyLibrariesConfig]) {
}

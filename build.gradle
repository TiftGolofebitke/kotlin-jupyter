
buildscript {
    ext.shadowJarVersion = "1.2.3"
//    ext.kotlinVersion = '1.1.0-dev-1514'
    ext.kotlinVersion = '1.1-SNAPSHOT'
    repositories {
        mavenLocal()
        maven { url 'https://repo.gradle.org/gradle/repo' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadowJarVersion"
    }
}

allprojects {
    apply plugin: 'kotlin'

    repositories {
        maven { url 'https://repo.gradle.org/gradle/repo' }
        jcenter()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
        testCompile 'junit:junit:4.12'
    }

    version = '0.1.0'

    ext.installPath = project.hasProperty('installPath') ? project.getProperty('installPath') : "${System.properties['user.home']}/.ipython/kernels/kotlin"
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-compiler:$kotlinVersion"
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'com.beust:klaxon:0.24'
}

apply plugin: 'com.github.johnrengelman.shadow'

configurations {
    deploy
}

dependencies {
    compile project(':jupyter-lib')
    runtime 'org.slf4j:slf4j-simple:1.7.21'
    compile 'org.zeromq:jeromq:0.3.5'
    compile 'com.beust:klaxon:0.24'
    deploy "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    deploy project(':jupyter-lib')
}

jar.manifest.attributes(
    'Main-class': 'org.jetbrains.kotlin.jupyter.IkotlinKt',
    'Implementation-Version': version
)

shadowJar {
    baseName = 'kotlin-jupyter-kernel'
    classifier = ''
    mergeServiceFiles()
}

task installKernel(type: Copy, dependsOn: shadowJar) {
    from shadowJar.outputs
    into installPath
}

task installLibs(type: Copy) {
    from configurations.deploy
    into "$installPath/lib"
}

task createSpecs(dependsOn: [installKernel, installLibs]) {
    String spec = new File('kernelspec/kernel.json.template').getText('UTF-8')
    File kernelFile = files { shadowJar }.singleFile
    spec = spec.replace("\${KERNEL_JAR_PATH}", "$installPath/${kernelFile.name}")
    String libsCp = files { configurations.deploy }.files.collect { "$installPath/lib/${it.name}" } .join(File.pathSeparator)
    spec = spec.replace("\${RUNTIME_CLASSPATH}", libsCp)
    new File( "$installPath/kernel.json" ).write( spec, 'UTF-8' )
}

task install(dependsOn: [installKernel, installLibs, createSpecs])
